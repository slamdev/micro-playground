apply plugin: 'pmd'

pmd {
    toolVersion = '5.8.1'
    ignoreFailures = project.hasProperty('skipStaticChecks') && project.skipStaticChecks as boolean
    consoleOutput = false
}

tasks.withType(Pmd) {
    if (name == 'pmdMain') {
        source = 'src/main/java'
        ruleSetFiles = files("${rootProject.projectDir}/config/pmd/ruleSetMain.xml")
        ruleSets = []
    } else {
        ruleSetFiles = files("${rootProject.projectDir}/config/pmd/ruleSetTest.xml")
        ruleSets = []
    }
    exclude '**/api/**'
}

apply plugin: 'checkstyle'

checkstyle {
    toolVersion = '8.5'
    ignoreFailures = project.hasProperty('skipStaticChecks') && project.skipStaticChecks as boolean
    showViolations = false
}

tasks.withType(Checkstyle) {
    if (name == 'checkstyleMain') {
        source = 'src/main/java'
        //noinspection GroovyAssignabilityCheck
        configFile = file("${rootProject.projectDir}/config/checkstyle/checkstyleMain.xml")
    } else {
        //noinspection GroovyAssignabilityCheck
        configFile = file("${rootProject.projectDir}/config/checkstyle/checkstyleTest.xml")
    }
    exclude '**/api/**'
}

apply plugin: 'findbugs'

findbugs {
    toolVersion = '3.0.1'
    ignoreFailures = project.hasProperty('skipStaticChecks') && project.skipStaticChecks as boolean
    effort = 'max'
    reportLevel = 'low'
}

tasks.withType(FindBugs) {
    if (name == 'findbugsMain') {
        excludeFilter = file("${rootProject.projectDir}/config/findbugs/excludeMain.xml")
    } else {
        excludeFilter = file("${rootProject.projectDir}/config/findbugs/excludeTest.xml")
    }
    pluginClasspath = project.configurations.findbugsPlugins
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

dependencies {
    findbugsPlugins 'com.mebigfatguy.fb-contrib:fb-contrib:7.0.5'
    findbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.7.1'
    findbugsPlugins 'com.digitalreasoning.sdp:findbugs-extensions-drs:1.1.0'
    checkstyle 'com.thomasjensen.checkstyle.addons:checkstyle-addons:5.2.0'
    checkstyle 'com.github.sevntu-checkstyle:sevntu-checks:1.25.0'
}
